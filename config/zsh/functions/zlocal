# -*- mode: sh -*- vim: ft=zsh
# function edit-zshrc-local {
local a b f
f=$HOME/.zshrc.local

echo "editing $f"
a=$(<"$f")
b=$(vipe <<<"$a") || return 1
if [[ "$a" == "$b" ]]; then
  echo "no changes detected"
  return 0
fi

if hash delta &>/dev/null; then
  diff -u <(printf '%s\n' "$a") <(printf '%s\n' "$b") | delta --paging=never
else
  diff -u <(printf '%s\n' "$a") <(printf '%s\n' "$b")
fi
echo

if ! zsh -n <<<"$b"; then
  echo >&2 "syntax error, aborting"
  return 1
fi

printf '%s\n' "$b" >"$f"
echo "wrote: $f"

if hash gh &>/dev/null; then
  local gistid=90b892b5069e95c2f893fab46177334a
  gh gist edit "$gistid" "$f"
  echo "updated: https://gist.github.com/$gistid"
fi

source "$f"
echo "sourced: $f"

echo 'done!'
# }
