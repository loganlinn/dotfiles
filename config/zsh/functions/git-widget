# -*- mode: sh -*- vim: ft=zsh
# function git-widget {
emulate -L zsh
{
case $(
  fzf +i --bind=one:accept --prompt="git: " --height=25% --nth=1 --accept-nth=1 <<'EOF'
b   branch
o   open...
p   prefix
s   status
t   toplevel
u   cdup
EOF
) in
b)
  local git_branch_name="$(git rev-parse --abbrev-ref HEAD 2> /dev/null)"
  LBUFFER+="${git_branch_name/HEAD/}"
  ;;
o) git-open-widget ;;
p) LBUFFER+="$(git rev-parse --show-prefix 2>/dev/null) " ;;
s)
  local git_status_files="$(git -c 'color.status=always' status -su 2> /dev/null)"
  if [[ -n "${git_status_files}" ]]; then
    local git_status_selected_files=(${(f)"$(echo ${git_status_files} | fzf --multi --ansi)"})
    for (( i=1; i<=${#git_status_selected_files}; i++ )); do
      LBUFFER+="${${(s: :)${git_status_selected_files[${i}]}}[2]}"
      if [[ ${i} -lt ${#git_status_selected_files} ]]; then
        LBUFFER+=' '
      fi
    done
  fi
  ;;
t) LBUFFER+="$(git rev-parse --show-toplevel 2>/dev/null)/" ;;
u) LBUFFER+="$(git rev-parse --show-cdup 2>/dev/null)" ;;
esac
} always {
  zle reset-prompt
}
# }
