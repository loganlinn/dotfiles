let g:Powerline_symbols='fancy'

"--------------------------------------------------------------------------------
" Tabs & Formatting
"--------------------------------------------------------------------------------
set tabstop=2
set shiftwidth=2
set softtabstop=2
set expandtab
set nu
set nobackup
set nowritebackup
set noswapfile
set mouse=a

" List chars
set listchars=""          " Reset the listchars
set listchars=tab:\ \     " a tab should display as "  ", trailing whitespace as "."
set listchars+=trail:.    " show trailing spaces as dots
set listchars+=extends:>  " The character to show in the last column when wrap is
                          " off and the line continues beyond the right of the screen
set listchars+=precedes:< " The character to show in the last column when wrap is
                          " off and the line continues beyond the right of the screen

" Highlight after 80 chars
highlight OverLength ctermbg=red ctermfg=white guibg=#592929
match OverLength /\%81v.\+/

" Huddler whitespace conventions: use tabs, 4 space width
fun! SetHuddlerWhitespace()
  "Ignore certain filetypes
  if &ft =~ 'clojure'
    return
  endif
  setlocal ts=4 sts=4 sw=4 noexpandtab
endfun
autocmd BufNewFile,BufRead */huddler/* call SetHuddlerWhitespace()

" Disable folding
set nofoldenable

" FileType specific
"" Java
autocmd FileType java setlocal expandtab

"" Clojure
augroup ft_clojure
  au!
  au FileType clojure setlocal lispwords+=describe,it,testing,facts,fact,provided,defroutes,defproject
  au FileType clojure setlocal lispwords+=with-connection,with-query-results,with-naming-strategy,with-quoted-identifiers
  au FileType clojure setlocal lispwords+=sql/with-connection,sql/with-query-results,sql/with-naming-strategy,sql/with-quoted-identifiers,sql/update-values,sql/insert-record,sql/insert-records,sql/insert!,sql/delete-rows

  " Indent top-level form.
  au FileType clojure nmap <buffer> <localleader>= mz99[(v%='z
augroup END

"--------------------------------------------------------------------------------
" Colors
"--------------------------------------------------------------------------------
""set t_Co=256
""set background=dark
"""color wombat256-logan
au VimEnter * highlight clear SignColumn

"--------------------------------------------------------------------------------
" Abbreviations
"--------------------------------------------------------------------------------
iab AlP ABCDEFGHIJKLMNOPQRSTUVWXYZ
iab MoN January February March April May June July August September October November December
iab MoO Jan Feb Mar Apr May Jun Jul Aug Sep Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec
iab NuM 12345678901234567890123456789012345678901234567890123456789012345678901234567890
iab RuL ----+----1----+----2----+----3----+----4----+----5----+----6----+----7----+----8----+----9----+----0

iabbrev ldis ಠ_ಠ
iabbrev lsad ಥ_ಥ
iabbrev lhap ಥ‿ಥ
iabbrev lmis ಠ‿ಠ

"--------------------------------------------------------------------------------
" Mappings
"--------------------------------------------------------------------------------
" Leaders
let mapleader = ","
let maplocalleader = "\\"

" Huddler
noremap <Leader>d :e $PROJECTS/huddler/common/defines.php<Enter>:vsp $PROJECTS/huddler/config/sites/logan.huddler.com/conf/config.overrides.php<Enter>:sp $PROJECTS/huddler/config/sites/logan.huddler.com/conf/config.db.php<Enter>
noremap <Leader>dt :tabnew<Enter><Leader>d
noremap <Leader>dc :e $PROJECTS/huddler/v2/system/application/libraries/HuddlerController.php<Enter>:vsp $PROJECTS/huddler/v2/system/application/libraries/HuddlerMobileController.php<Enter>:vsp $PROJECTS/huddler/v2/system/application/libraries/Huddler/Controller.php<Enter>

" Align
noremap <Leader>= :Align=<Enter>
noremap <Leader>- :Align=><Enter>
noremap <Leader>] <c-w>]<c-w>H

" Last tab binding
let g:lasttab = 1
nmap <c-w>; :exe "tabn ".g:lasttab<cr>
au TabLeave * let g:lasttab = tabpagenr()

" Safely alias :we to :w
cnoreabbrev <expr> we ((getcmdtype() is# ':' && getcmdline() is# 'w')?('we'):('w'))

" From https://github.com/sjl/dotfiles/blob/master/vim/vimrc
" Fuck you, help key
noremap  <F1> :set invfullscreen<CR>
inoremap <F1> <ESC>:set invfullscreen<CR>a
" Emacs bindings in command line mode
cnoremap <c-a> <home>
cnoremap <c-e> <end>
" Formatting, TextMate-style
nnoremap Q gqip
vnoremap Q gq
" Don't move on *
nnoremap * *<c-o>
" Keep search matches in the middle of the window.
nnoremap n nzzzv
nnoremap N Nzzzv
" Heresy
inoremap <c-a> <esc>I
inoremap <c-e> <esc>A
"--------------------------------------------------------------------------------
" Plugin Settings
"--------------------------------------------------------------------------------
let $JS_CMD='node'

" VimClojure
let g:vimclojure#WantNailgun = 1
let g:vimclojure#NailgunClient = $HOME."/vimclojure/client/ng"
let g:vimclojure#HighlightBuiltins = 1
let g:vimclojure#DynamicHighlighting = 1
let g:vimclojure#ParenRainbow = 1
let g:vimclojure#SetupKeyMapEvalToplevel = 0
let g:vimclojure#SetupKeyMapRunTests = 0
let vimclojure#SplitPos = "right"

" Paredit
"let g:paredit_electric_return=1

" Syntastic
let g:syntastic_auto_loc_list = 1
let g:syntastic_javascript_jsl_conf = "-conf ~/.jslconf"
let g:syntastic_mode_map = {
    \ 'mode': 'active',
    \ 'active_filetypes': [],
    \ 'passive_filetypes': ['sass', 'scss']
  \}

" CtrlP
let g:ctrlp_switch_buffer = 0
let g:ctrlp_user_command = {
    \ 'types': {
      \ 1: ['.git', 'cd %s && git ls-files'],
      \ 2: ['.hg', 'hg --cwd %s locate -I .'],
      \ },
    \ 'fallback': 'find %s -type f'
  \ }

" Ack
nnoremap <leader>a :Ack!<space>
let g:ackprg = 'ag --nogroup --nocolor --column'

" Tagbar
"" Move focus to tagbar when it opens to make it usable for windows on right side
let g:tagbar_autofocus = 1

"--------------------------------------------------------------------------------
" General Settings & Modifications
"--------------------------------------------------------------------------------
" Disable session dialog
let g:session_autosave='no'
" Enable filetype plugin
filetype plugin on

" Tabline modifications
if has('gui')
  set guioptions-=e
endif
if exists("+showtabline")
  function MyTabLine()
    let s = ''
    let t = tabpagenr()
    let i = 1
    while i <= tabpagenr('$')
      let buflist = tabpagebuflist(i)
      let winnr = tabpagewinnr(i)
      let s .= '%' . i . 'T'
      let s .= (i == t ? '%1*' : '%2*')
      let s .= ' '
      let s .= i . ':'
      let s .= winnr . '/' . tabpagewinnr(i,'$')
      let s .= ' %*'
      let s .= (i == t ? '%#TabLineSel#' : '%#TabLine#')
      let bufnr = buflist[winnr - 1]
      let file = bufname(bufnr)
      let buftype = getbufvar(bufnr, 'buftype')
      if buftype == 'nofile'
        if file =~ '\/.'
          let file = substitute(file, '.*\/\ze.', '', '')
        endif
      else
        let file = fnamemodify(file, ':p:t')
      endif
      if file == ''
        let file = '[No Name]'
      endif
      let s .= file
      let i = i + 1
    endwhile
    let s .= '%T%#TabLineFill#%='
    let s .= (tabpagenr('$') > 1 ? '%999XX' : 'X')
    return s
  endfunction
  set stal=2
  set tabline=%!MyTabLine()
endif

" PIV
let g:DisableAutoPHPFolding = 1

" vim-indent-guides
let g:indent_guides_enable_on_vim_startup = 0
