################################################################################
# Profile ZSH startup -- do not place anything above this line
################################################################################
# zmodload zsh/zprof

################################################################################
# environment variables
################################################################################

export ZSH=$HOME/.dotfiles
export PROJECTS=$HOME/code

################################################################################
# load zsh dotfiles
################################################################################

typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

################################################################################
# fzf
################################################################################
if [ -f "/usr/local/opt/fzf/shell/key-bindings.zsh" ]; then
  source "/usr/local/opt/fzf/shell/key-bindings.zsh"
fi

################################################################################
# pyenv
################################################################################

if (( $+commands[pyenv] )); then
  eval "$(pyenv init -)"
  if (( $+commands[pyenv-virtualenv-init] )); then
    eval "$(pyenv virtualenv-init -)"
  fi
fi

################################################################################
# direnv
################################################################################

if (( $+commands[direnv] )); then
  eval "$(direnv hook zsh)"
fi


################################################################################
# jenv
################################################################################

if (( $+commands[jenv] )); then
  eval "$(jenv init -)"
fi

################################################################################
# hub
################################################################################

if (( $+commands[hub] )); then
  eval "$(hub alias -s)"
fi

################################################################################
# completions
################################################################################

autoload -U +X bashcompinit && bashcompinit || true
complete -o nospace -C /usr/local/bin/mc mc || true

################################################################################
# localrc for secrets and secret things
################################################################################

if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

################################################################################
# Profile ZSH startup -- do not place anything below this line
################################################################################
# zprof
