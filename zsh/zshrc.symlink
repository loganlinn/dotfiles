################################################################################
# Profile ZSH startup -- do not place anything above this line
################################################################################
# zmodload zsh/zprof
################################################################################

export ZSH=$HOME/.dotfiles
export PROJECTS=$HOME/code

################################################################################
# localrc for secrets and secret things
################################################################################

if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

################################################################################
# load zsh dotfiles
################################################################################

typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

################################################################################
# fzf
################################################################################
if [ -f "/usr/local/opt/fzf/shell/key-bindings.zsh" ]; then
  source "/usr/local/opt/fzf/shell/key-bindings.zsh"
fi

################################################################################
# pyenv
################################################################################

eval "$(pyenv init -)"
if which pyenv-virtualenv-init > /dev/null; then
  eval "$(pyenv virtualenv-init -)"
fi

################################################################################
# jenv
################################################################################

eval "$(jenv init -)"

################################################################################
# alias hub as git
################################################################################

eval "$(hub alias -s)"

################################################################################
# Profile ZSH startup -- do not place anything below this line
################################################################################

# zprof

################################################################################
# party zone (a.k.a where install scripts think they are welcome)
################################################################################
autoload -U +X bashcompinit && bashcompinit
complete -o nospace -C /usr/local/bin/mc mc
